Cheesy Clay Grasshopper

Medium

# Use call() instead of transfer() when sending ETH in withdrawAmountVariablePending()

## Summary
When sending ETH in `withdrawAmountVariablePending()`, the transaction can revert if msg.sender is a contract.

## Vulnerability Detail
If a fee receiver finalizes a variable user's request through `feeReceiverFinalizeVaultOngoingVariableWithdrawals()`, the user can redeem their assets with `withdrawAmountVariablePending()` but if the variable side user is a smart contract (account abstraction wallet, gnosis safe etc.) the redeem transaction will revert if it requires more than 2300 gas sent with transfer(). 
Also, if that user sent another request after (but before the vault ends), they won't be able to redeem any rewards because of checks in `finalizeVaultOngoingVariableWithdrawals()` and `vaultEndedWithdraw()`
```solidity
function feeReceiverFinalizeVaultOngoingVariableWithdrawals(address user) external {
    require(msg.sender == protocolFeeReceiver, "IFR");
    uint256[] memory requestIds = variableToVaultOngoingWithdrawalRequestIds[user];
    require(requestIds.length != 0, "WNR");

    delete variableToVaultOngoingWithdrawalRequestIds[user];

    uint256 amountWithdrawn = claimWithdrawals(user, requestIds);

    uint256 protocolFee = applyProtocolFee(amountWithdrawn);

    uint256 sendAmount = amountWithdrawn + calculateVariableFeeEarningsShareWithUser(user) - protocolFee;
    variableToPendingWithdrawalAmount[user] += sendAmount;
  }

function withdrawAmountVariablePending() public {
    uint256 amount = variableToPendingWithdrawalAmount[msg.sender];
    variableToPendingWithdrawalAmount[msg.sender] = 0;
    payable(msg.sender).transfer(amount);
  }
```

```solidity
function finalizeVaultOngoingVariableWithdrawals() external {
    uint256[] memory requestIds = variableToVaultOngoingWithdrawalRequestIds[msg.sender];
    if(variableToPendingWithdrawalAmount[msg.sender] != 0) {
 @>   withdrawAmountVariablePending();
      if(requestIds.length == 0) {
        return;
      }
    }
    ...
  }
```
```solidity
function vaultEndedWithdraw(uint256 side) internal {
    ...
    if (side == FIXED) {
     ...
    } else {
      require(variableToVaultOngoingWithdrawalRequestIds[msg.sender].length == 0, "WAR");
      ...
  }
}
```

## Impact
Variable side users with smart contract wallets could lose their rewards if their request is finalized by the fee receiver, Furthermore, they won't be able to redeem later requests if sent while the vault is ongoing.

## Code Snippet
https://github.com/sherlock-audit/2024-08-saffron-finance/blob/main/lido-fiv/contracts/LidoVault.sol#L637-L657

## Tool used
Manual Review

## Recommendation
Use call() instead of transfer() when sending ETH
```solidity
function withdrawAmountVariablePending() public {
    uint256 amount = variableToPendingWithdrawalAmount[msg.sender];
    variableToPendingWithdrawalAmount[msg.sender] = 0;
    (bool ok, ) = payable(msg.sender).call{value: amount}("");
    require(ok, "call failed");
  }
```