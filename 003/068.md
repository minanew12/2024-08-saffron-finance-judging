Digital Chocolate Hedgehog

Medium

# Deprecated ETH Transfer Method in `LidoVault::withdrawAmountVariablePending` May Prevent Variable Side Pending Rewards from Being Withdrawn.

### Summary

The `LidoVault::withdrawAmountVariablePending` function is responsible for allowing variable side depositors to withdraw pending rewards once the vault has started accruing stacking rewards. However, the function currently relies on the deprecated `payable(address).transfer(amount)` method, which imposes a fixed gas stipend of only 2,300 units. This amount may be inadequate for certain contracts that require additional logic to process the transfer, potentially leading to a revert and preventing the withdrawal of rewards.

The rationale behind using the `transfer` method in the `withdrawAmountVariablePending` function is unclear, especially since the rest of the code employs the `call` method. This choice might stem from the way the `finalizeVaultOngoingVariableWithdrawals` function operates; it first calls `withdrawAmountVariablePending` and then updates several state variables. While using `transfer` helps mitigate reentrancy risks, it inadvertently restricts some contracts from interacting with the protocol and withdrawing their variable side rewards, as previously mentioned.

[https://github.com/sherlock-audit/2024-08-saffron-finance/blob/main/lido-fiv/contracts/LidoVault.sol#L653-L657](https://github.com/sherlock-audit/2024-08-saffron-finance/blob/main/lido-fiv/contracts/LidoVault.sol#L653-L657)

```solidity
  function withdrawAmountVariablePending() public {
    uint256 amount = variableToPendingWithdrawalAmount[msg.sender];
    variableToPendingWithdrawalAmount[msg.sender] = 0;
@>  payable(msg.sender).transfer(amount);
  }
```

[https://github.com/sherlock-audit/2024-08-saffron-finance/blob/main/lido-fiv/contracts/LidoVault.sol#L610C12-L633](https://github.com/sherlock-audit/2024-08-saffron-finance/blob/main/lido-fiv/contracts/LidoVault.sol#L610C12-L633)

```solidity
 function finalizeVaultOngoingVariableWithdrawals() external {
    uint256[] memory requestIds = variableToVaultOngoingWithdrawalRequestIds[msg.sender];
    if(variableToPendingWithdrawalAmount[msg.sender] != 0) {
@>    withdrawAmountVariablePending();
@>   // Using `call` instead of `transfer` may introduce a reentrancy vulnerability given that fact that after this call
@>   // some storage variables are updated 

      if(requestIds.length == 0) {
        return;
      }
    }
    require(requestIds.length != 0, "WNR");

    delete variableToVaultOngoingWithdrawalRequestIds[msg.sender];

    uint256 amountWithdrawn = claimWithdrawals(msg.sender, requestIds);

    uint256 protocolFee = applyProtocolFee(amountWithdrawn);

    uint256 sendAmount = amountWithdrawn + calculateVariableFeeEarningsShare() - protocolFee;

    bool _isEnded = isEnded();
    transferWithdrawnFunds(msg.sender, sendAmount);

    emit LidoWithdrawalFinalized(msg.sender, requestIds, VARIABLE, true, _isEnded);
    emit VariableFundsWithdrawn(sendAmount, msg.sender, true, _isEnded);
  }
```

### Root Cause

In https://github.com/sherlock-audit/2024-08-saffron-finance/blob/main/lido-fiv/contracts/LidoVault.sol#L656 the contract uses `transfer` instead of a low level `call` function.

### Internal pre-conditions

_No response_

### External pre-conditions

A contract that calls `LidoVault::finalizeVaultOngoingVariableWithdrawals` or `LidoVault::withdrawAmountVariablePending`  contains additional logic within its `fallback` or `receive` function(s) requiring more than 2,300 gas to handle the processing of variable side stacking reward claims.

### Attack Path

_No response_

### Impact

A contract that invokes `LidoVault::finalizeVaultOngoingVariableWithdrawals` or `LidoVault::withdrawAmountVariablePending` will experience a transaction revert, preventing it from successfully claiming its rewards.

### PoC

_No response_

### Mitigation


To address this vulnerability, it is advisable to replace the `transfer` method with a low-level `.call()` for transferring Ether in `withdrawAmountVariablePending`. Additionally, the `finalizeVaultOngoingVariableWithdrawals` function could be refactored to consolidate the two ETH transfers into a single call at the end of its execution. This approach would enhance the reliability of the withdrawal process while maintaining security.

```diff
-  function withdrawAmountVariablePending() public {
+  function withdrawAmountVariablePending() external {
    uint256 amount = variableToPendingWithdrawalAmount[msg.sender];
    variableToPendingWithdrawalAmount[msg.sender] = 0;

+   (bool sent, ) = msg.sender.call{value: amount}("");
+   require(sent, "ETF");
-   payable(msg.sender).transfer(amount);
}
```

```diff
function finalizeVaultOngoingVariableWithdrawals() external {
    uint256[] memory requestIds = variableToVaultOngoingWithdrawalRequestIds[msg.sender];

+   uint256 amountOne;

    if (variableToPendingWithdrawalAmount[msg.sender] != 0) {
+     amountOne = variableToPendingWithdrawalAmount[msg.sender];
+     variableToPendingWithdrawalAmount[msg.sender] = 0;

-     withdrawAmountVariablePending();
-     if(requestIds.length == 0) {
-       return;
-     }
    }

+   uint256 amountTwo;

-   require(requestIds.length != 0, "WNR");
+   if (requestIds.length != 0) {
      delete variableToVaultOngoingWithdrawalRequestIds[msg.sender];

      uint256 amountWithdrawn = claimWithdrawals(msg.sender, requestIds);

      uint256 protocolFee = applyProtocolFee(amountWithdrawn);

-     uint256 sendAmount = amountWithdrawn + calculateVariableFeeEarningsShare() - protocolFee;
+     amountTwo = amountWithdrawn + calculateVariableFeeEarningsShare() - protocolFee;

      bool _isEnded = isEnded();
-     transferWithdrawnFunds(msg.sender, sendAmount);

      emit LidoWithdrawalFinalized(msg.sender, requestIds, VARIABLE, true, _isEnded);
-     emit VariableFundsWithdrawn(sendAmount, msg.sender, true, _isEnded);
+     emit VariableFundsWithdrawn(amountTwo, msg.sender, true, _isEnded);
+   }

+   if (amountOne + amountTwo > 0) {
+     transferWithdrawnFunds(msg.sender, amountOne + amountTwo);
+   }
}
```
