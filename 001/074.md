Cuddly Scarlet Antelope

High

# Variable side users will pay more protocolFee than they have to

## Summary

The `protocolFee` is charged from the yield, which is being distributed to the `VARIABLE` side. The problem is that currently the protocol has implemented this in such way where the `VARIABLE` users will have to pay much more `protocolFee` than needed because the math is wrong. 

## Vulnerability Detail

We will go through a protocol flow where some variable users just withdraw when the vault is ongoing to collect part of their earnings, while others just wait for the end.

The users who want to withdraw on chunks will just call the withdraw() function and they will end up in the if statement for the ongoing vault here:

https://github.com/sherlock-audit/2024-08-saffron-finance/blob/main/lido-fiv/contracts/LidoVault.sol#L533

```solidity
 function withdraw(uint256 side) external {

  ... MORE CODE

  else {
        if (msg.sender == protocolFeeReceiver && appliedProtocolFee > 0) {
          
          require(variableToVaultOngoingWithdrawalRequestIds[msg.sender].length == 0, "WAR");
          return protocolFeeReceiverWithdraw();
        }

        if (variableToVaultOngoingWithdrawalRequestIds[msg.sender].length == 0) {
          uint256 lidoStETHBalance = stakingBalance();
          uint256 fixedETHDeposits = fixedSidestETHOnStartCapacity;

          // staking earnings have accumulated on Lido
          if (lidoStETHBalance > fixedETHDeposits + minStETHWithdrawalAmount()) {
          
            uint256 currentStakes = stakingShares();
           
            (uint256 currentState, uint256 ethAmountOwed) = calculateVariableWithdrawState(
              (lidoStETHBalance.mulDiv(currentStakes + withdrawnStakingEarningsInStakes, currentStakes) -
                fixedETHDeposits),
              variableToWithdrawnStakingEarningsInShares[msg.sender].mulDiv(lidoStETHBalance, currentStakes)
            );
            if (ethAmountOwed >= minStETHWithdrawalAmount()) {
              // estimate protocol fee and update total - will actually be applied on withdraw finalization
>>            uint256 protocolFee = ethAmountOwed.mulDiv(protocolFeeBps, 10000);
>>            totalProtocolFee += protocolFee;
              uint256 stakesAmountOwed = lido.getSharesByPooledEth(ethAmountOwed);

              withdrawnStakingEarnings += ethAmountOwed - protocolFee;
              withdrawnStakingEarningsInStakes += stakesAmountOwed;

              variableToWithdrawnStakingEarnings[msg.sender] += ethAmountOwed - protocolFee;
              variableToWithdrawnStakingEarningsInShares[msg.sender] += stakesAmountOwed;
              variableToWithdrawnProtocolFee[msg.sender] += protocolFee;
              variableToVaultOngoingWithdrawalRequestIds[msg.sender] = requestWithdrawViaETH(msg.sender, ethAmountOwed);

              emit LidoWithdrawalRequested(
                msg.sender,
                variableToVaultOngoingWithdrawalRequestIds[msg.sender],
                VARIABLE,
                true,
                false
              );
            }
          }
        }

       ... MORE CODE
 }

```
What we do with the `protocolFee` in this function is that we calculate it based on the amount the user has to withdraw and we add it to the `totalProtocolFee`. The `totalProtocolFee` is a fee that should be payed by the `VARIABLE` side at the end.

Then what the user will do is he will call `finalizeVaultOngoingVariableWithdrawals()` to collect his earnings:

https://github.com/sherlock-audit/2024-08-saffron-finance/blob/main/lido-fiv/contracts/LidoVault.sol#L626
```solidity

 function finalizeVaultOngoingVariableWithdrawals() external {
    uint256[] memory requestIds = variableToVaultOngoingWithdrawalRequestIds[msg.sender];
    if (variableToPendingWithdrawalAmount[msg.sender] != 0) {
      withdrawAmountVariablePending();
      if (requestIds.length == 0) {
        return;
      }
    }
    require(requestIds.length != 0, "WNR");

    delete variableToVaultOngoingWithdrawalRequestIds[msg.sender];

>>  uint256 amountWithdrawn = claimWithdrawals(msg.sender, requestIds);

>>  uint256 protocolFee = applyProtocolFee(amountWithdrawn);

>>  uint256 sendAmount = amountWithdrawn + calculateVariableFeeEarningsShare() - protocolFee;

    bool _isEnded = isEnded();
>>  transferWithdrawnFunds(msg.sender, sendAmount);

    emit LidoWithdrawalFinalized(msg.sender, requestIds, VARIABLE, true, _isEnded);
    emit VariableFundsWithdrawn(sendAmount, msg.sender, true, _isEnded);
  }

```

Here is where part of the problem occurs. As we can see we are calculating the `protocolFee` again and we take it from the amount the user has to receive. Basically he pays the `protocolFee` from his earnings that he has to but the `totalProtocolFee` is not being updated.

The flow continues and the vault has ended, now the remaining yield can be collected by the `VARIALBE` side. What happens now is someone will call `withdraw()` which will invoke `vaultEndedWithdraw(side)`, because it is the first time it is being called it will enter the first if statement and generate the `vaultEndedWithdrawalRequestIds` and simply return here:

https://github.com/sherlock-audit/2024-08-saffron-finance/blob/main/lido-fiv/contracts/LidoVault.sol#L722
```solidity

if (vaultEndedWithdrawalRequestIds.length == 0 && !vaultEndedWithdrawalsFinalized) {
      emit VaultEnded(block.timestamp, msg.sender);
      if (stakingBalance() < minStETHWithdrawalAmount()) {
        claimOngoingFixedWithdrawals();
        vaultEndingStakesAmount = stakingShares();
        vaultEndingETHBalance = stakingBalance();
        // not enough staking ETH to withdraw just override vault ended state and continue the withdraw
        vaultEndedWithdrawalsFinalized = true;
      } else {
        vaultEndingStakesAmount = stakingShares();
        vaultEndingETHBalance = stakingBalance();
>>      vaultEndedWithdrawalRequestIds = requestEntireBalanceWithdraw(msg.sender);

        emit LidoWithdrawalRequested(msg.sender, vaultEndedWithdrawalRequestIds, side, true, true);
>>      // need to call finalizeVaultEndedWithdrawals once request is processed
        return;
      }

```


Then the `finalizeVaultEndedWithdrawals()` has to be called

https://github.com/sherlock-audit/2024-08-saffron-finance/blob/main/lido-fiv/contracts/LidoVault.sol#L683
```solidity

function finalizeVaultEndedWithdrawals(uint256 side) external {
    require(side == FIXED || side == VARIABLE, "IS");
   
    if (vaultEndedWithdrawalsFinalized) {
      return vaultEndedWithdraw(side);
    }
    require(vaultEndedWithdrawalRequestIds.length != 0 && !vaultEndedWithdrawalsFinalized, "WNR");

    vaultEndedWithdrawalsFinalized = true;

    // claim any ongoing fixed withdrawals too
    claimOngoingFixedWithdrawals();
    //@note this collects the whole yield earned, not only for the msg.sender
 >> uint256 amountWithdrawn = claimWithdrawals(msg.sender, vaultEndedWithdrawalRequestIds);
  
    uint256 fixedETHDeposit = fixedSidestETHOnStartCapacity;

    if (amountWithdrawn > fixedETHDeposit) {
      vaultEndedStakingEarnings = amountWithdrawn - fixedETHDeposit;
      vaultEndedFixedDepositsFunds = fixedETHDeposit;
    } else {
      vaultEndedFixedDepositsFunds = amountWithdrawn;
    }
    
>>  uint256 protocolFee = applyProtocolFee(vaultEndedStakingEarnings);
>>  vaultEndedStakingEarnings -= protocolFee;
  
    emit LidoWithdrawalFinalized(msg.sender, vaultEndedWithdrawalRequestIds, side, true, true);

>>  return vaultEndedWithdraw(side);
  }

```

Here we remove the `protocolFee` from the remaining yield (remaining because some if it was claimed while the vault was ongoing) which is fine, but then when `vaultEndedWithdraw(SIDE.VARIABLE)` is invoked again for the user, so now he can collect the yield because the requests have been proccessed

we go into this else statement:

https://github.com/sherlock-audit/2024-08-saffron-finance/blob/main/lido-fiv/contracts/LidoVault.sol#L775
```solidity
function vaultEndedWithdraw(uint256 side) internal {

  ...MORE CODE

 else {
      require(variableToVaultOngoingWithdrawalRequestIds[msg.sender].length == 0, "WAR");

      if (msg.sender == protocolFeeReceiver && appliedProtocolFee > 0) {
        return protocolFeeReceiverWithdraw();
      }

      uint256 bearerBalance = variableBearerToken[msg.sender];
      require(bearerBalance > 0, "NBT");

      // Return proportional share of both earnings to caller
      uint256 stakingShareAmount = 0;

>>    uint256 totalEarnings = vaultEndingETHBalance.mulDiv(withdrawnStakingEarningsInStakes,      vaultEndingStakesAmount) -
        totalProtocolFee +
        vaultEndedStakingEarnings;

      if (totalEarnings > 0) {
        (uint256 currentState, uint256 stakingEarningsShare) = calculateVariableWithdrawState(
>>        totalEarnings,
          variableToWithdrawnStakingEarningsInShares[msg.sender].mulDiv(vaultEndingETHBalance, vaultEndingStakesAmount)
        );
>>      stakingShareAmount = stakingEarningsShare;
        variableToWithdrawnStakingEarningsInShares[msg.sender] = currentState.mulDiv(
          vaultEndingStakesAmount,
          vaultEndingETHBalance
        );
        variableToWithdrawnStakingEarnings[msg.sender] = currentState;
      }

      uint256 feeShareAmount = 0;
      if (withdrawnFeeEarnings + feeEarnings > 0) {
        feeShareAmount = calculateVariableFeeEarningsShare();
      }

      variableBearerToken[msg.sender] -= bearerBalance;
      variableBearerTokenTotalSupply -= bearerBalance;

>>    uint256 sendAmount = stakingShareAmount + feeShareAmount;
>>    transferWithdrawnFunds(msg.sender, sendAmount);

      emit VariableFundsWithdrawn(sendAmount, msg.sender, true, true);
      return;
    }
```

As we can see we are removing the `totalProtocolFee` from the `totalEarnings` which are then used to calculate the user's amount that he has to withdraw. This makes the formula completely wrong and makes the `VARIABLE` side pay for fees again. This `totalProtocolFee` was already paid in the `finalizeVaultOngoingVariableWithdrawals()` and is removed from the variable `vaultEndedStakingEarnings`. You already took the fee from the chunks that people were withdrawing when the vault is ongoing and in the `finalizeVaultEndedWithdrawals()` you took the fee from the `vaultEndedStakingEarnings` which means that the fee is already collected and should not be subtracted in the `vaultEndedWithdraw()` when calculating the `totalEarnings`

## Impact

High because users pay more `protocolFee` than what they need to, which means they incur loses and don't receive 100% of the yield that they deserve and this happens everytime so the likelihood is also high

## Code Snippet

https://github.com/sherlock-audit/2024-08-saffron-finance/blob/main/lido-fiv/contracts/LidoVault.sol#L533

https://github.com/sherlock-audit/2024-08-saffron-finance/blob/main/lido-fiv/contracts/LidoVault.sol#L626

https://github.com/sherlock-audit/2024-08-saffron-finance/blob/main/lido-fiv/contracts/LidoVault.sol#L722

https://github.com/sherlock-audit/2024-08-saffron-finance/blob/main/lido-fiv/contracts/LidoVault.sol#L683

https://github.com/sherlock-audit/2024-08-saffron-finance/blob/main/lido-fiv/contracts/LidoVault.sol#L775

## Tool used

Manual Review

## Recommendation

Remove the `totalProtocolFee` when calculating the `totalEarnings` at the end in `vaultEndedWithdraw()` because the total fees are already subtracted from the yield earned.

```diff

- uint256 totalEarnings = vaultEndingETHBalance.mulDiv(withdrawnStakingEarningsInStakes, vaultEndingStakesAmount) - totalProtocolFee + vaultEndedStakingEarnings;

+ uint256 totalEarnings = vaultEndingETHBalance.mulDiv(withdrawnStakingEarningsInStakes, vaultEndingStakesAmount) + vaultEndedStakingEarnings;

```