Amusing Flaxen Bull

High

# Unaccounted protocol fee will lead to funds getting locked

### Summary

Not accounting of a protocol fee on variable side withdraw will decrease the user's earnings and lock the funds in the contract.

### Root Cause

In [vaultEndedWithdraw()](https://github.com/sherlock-audit/2024-08-saffron-finance/blob/main/lido-fiv/contracts/LidoVault.sol#L780) when calculating the `stakingEarningsShare`, the [calculateVariableWithdrawState()](https://github.com/sherlock-audit/2024-08-saffron-finance/blob/main/lido-fiv/contracts/LidoVault.sol#L859) is used. This function accepts the total earnings and the previously withdrawn earnings. The previously withdrawn earnings are calculated using `variableToWithdrawnStakingEarningsInShares[msg.sender]`, which is tracking the shares equivalent of the amount withdrawn. But in [withdraw()](https://github.com/sherlock-audit/2024-08-saffron-finance/blob/main/lido-fiv/contracts/LidoVault.sol#L531-L545) we can see that the share amount also includes the protocol fees, for which we have not accounted. 

### Internal pre-conditions

1. At least one variable side withdrawal to be made before the end of the vault duration.

### External pre-conditions

_No response_


### Attack Path

1. Fixed side deposit and Variable side deposits are made, so that the vault is started.
2. Rewards are accrued because of the staking.
3. Variable side user calls `withdraw(1)`, which will create withdraw request for a part of these rewards.
`variableToWithdrawnStakingEarningsInStakes[msg.sender]` is updated to represent the shares that will be burnt, and `variableToWithdrawnStakingEarnings[msg.sender]` is updated to represent the withdrawn amount without the protocol fees.
4. When the duration ends, the staking is finalized, all staked amount is unstaked and all withdraw requests are claimed.
5. Any user participating in the variable side call `withdraw(1)`, and according to the calculations he will receive less than he had to.
6. All users have withdrawn and the `protocolFeeRecipient` withdraws all fees

### Impact

- The user will receive less than expected rewards. The exact amount is `(protocolFee1 + protocolFee2 ... + protoclFeeN)`, where `N` is the number of withdrawals made before vault duration ends.
- The amount that the user will not receive will be left inaccessible in the contract.

### PoC

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.20;

import "@openzeppelin/contracts/utils/math/Math.sol";

import {Test, console2} from "forge-std/Test.sol";

import {VaultFactory} from "src/VaultFactory.sol";
import {LidoVault} from "src/LidoVault.sol";
import {ILido} from "src/interfaces/ILido.sol";
import {MockLido} from "src/mocks/MockLido.sol";
import {MockLidoWithdrawalQueue} from "src/mocks/MockLidoWithdrawalQueue.sol";

contract PoC is Test {
    using Math for uint256;

    address payable constant LIDO = payable(0xae7ab96520DE3A18E5e111B5EaAb095312D7fE84);
    address payable constant LIDO_WITHDRAWAL_QUEUE = payable(0x889edC2eDab5f40e902b864aD4d7AdE8E412F9B1);

    VaultFactory vf;
    LidoVault lv;

    address fixedUser = makeAddr("fixedUser");
    address variableUser1 = makeAddr("variableUser1");
    address variableUser2 = makeAddr("variableUser2");
    address protocolFeeReceiver = makeAddr("protocolFeeReceiver");

    uint256 fixedSideCapacity = 1 ether;
    uint256 variableSideCapacity = 1 ether;
    uint256 duration = 7 days;

    function setUp() public {
        // Deploy the Lido mocks on the respective addresses
        deployCodeTo("MockLido", LIDO);
        deployCodeTo("MockLidoWithdrawalQueue", LIDO_WITHDRAWAL_QUEUE);

        MockLidoWithdrawalQueue(LIDO_WITHDRAWAL_QUEUE).initialize(LIDO);

        deal(LIDO_WITHDRAWAL_QUEUE, 100 ether);

        // Deploy the vault factory
        vm.prank(protocolFeeReceiver);
        vf = new VaultFactory(100, 100);

        uint256 vaultId = vf.nextVaultId();

        vf.createVault(fixedSideCapacity, duration, variableSideCapacity);
        (, address addr) = vf.vaultInfo(vaultId);

        lv = LidoVault(payable(addr));

        deal(fixedUser, 10 ether);
        deal(variableUser1, 10 ether);
        deal(variableUser2, 10 ether);
    }

    function test2() public {
        uint256 protocolStartingBalance = address(lv).balance;
        // Fixed side deposit
        vm.prank(fixedUser);
        lv.deposit{value: 1 ether}(0);

        // Variable side deposit
        vm.prank(variableUser1);
        lv.deposit{value: 0.5 ether}(1);

        vm.prank(variableUser2);
        lv.deposit{value: 0.5 ether}(1);

        // Validate the vault have started
        assert(lv.isStarted() == true);
        assert(lv.isEnded() == false);

        // Claim the fixed premium
        vm.prank(fixedUser);
        lv.claimFixedPremium();

        // Add rewards
        MockLido(LIDO).addStakingEarnings(1 ether);

        // Create withdraw variable request
        vm.prank(variableUser1);
        lv.withdraw(1);

        //Validate the request has been created
        assert(lv.variableToWithdrawnStakingEarningsInShares(variableUser1) > 0);
        assert(lv.totalProtocolFee() > 0);
        uint256 firstWithdrawalFee = lv.totalProtocolFee();

        // Finalize the withdraw
        uint256 startBalance = variableUser1.balance;
        vm.prank(variableUser1);
        lv.finalizeVaultOngoingVariableWithdrawals();
        uint256 endBalance = variableUser1.balance;

        uint256 withdrawn = endBalance - startBalance;

        // Add rewards again
        MockLido(LIDO).addStakingEarnings(1 ether);

        // Roll the time to the end of the vault
        skip(duration + 1);

        // Ensure the duration has ended
        assert(lv.isEnded() == true);

        // Finalize the vault
        vm.prank(fixedUser);
        lv.withdraw(0); // Triggers the withdraw from the LIDO of the whole balance

        // Claim the balance withdrawn and update the state variables
        vm.prank(fixedUser);
        lv.finalizeVaultEndedWithdrawals(0);

        // Ensure the withdrawals of the whole balance are settled
        assert(lv.vaultEndedWithdrawalsFinalized() == true);

        // Calculating the amount that has been withdrawn before the vault ended
        // using the formula from the LidoVault.sol::vaultEndedWithdraw() line 780
        uint256 withdrawnCalculations = lv.variableToWithdrawnStakingEarningsInShares(variableUser1).mulDiv(
            lv.vaultEndingETHBalance(), lv.vaultEndingStakesAmount()
        );

        assert(withdrawn < withdrawnCalculations);

        // Everyone withdraws
        vm.prank(variableUser1);
        lv.withdraw(1);

        vm.prank(variableUser2);
        lv.withdraw(1);

        // Claim the fees
        vm.prank(protocolFeeReceiver);
        lv.withdraw(1);

        assert(lv.appliedProtocolFee() == 0);

        uint256 protocolFinalBalance = address(lv).balance;

        uint256 finalBalance = protocolFinalBalance - protocolStartingBalance;

        // Validate that exactly the fee of the first withdrawal is remaining
        // It will be locked in the contract
        assert(finalBalance != 0);
        assert(finalBalance == firstWithdrawalFee);
    }
}
```

### Mitigation

In the [LidoVault.sol](https://github.com/sherlock-audit/2024-08-saffron-finance/blob/main/lido-fiv/contracts/LidoVault.sol#L762-L786)
```diff
  } else {
            require(variableToVaultOngoingWithdrawalRequestIds[msg.sender].length == 0, "WAR");

            if (msg.sender == protocolFeeReceiver && appliedProtocolFee > 0) {
                return protocolFeeReceiverWithdraw();
            }

            uint256 bearerBalance = variableBearerToken[msg.sender];
            require(bearerBalance > 0, "NBT");

            // Return proportional share of both earnings to caller
            uint256 stakingShareAmount = 0;

            uint256 totalEarnings = vaultEndingETHBalance.mulDiv(
                withdrawnStakingEarningsInStakes, vaultEndingStakesAmount
            ) - totalProtocolFee + vaultEndedStakingEarnings; 

            if (totalEarnings > 0) {
-                (uint256 currentState, uint256 stakingEarningsShare) = calculateVariableWithdrawState(
-                    totalEarnings,
-                   variableToWithdrawnStakingEarningsInShares[msg.sender].mulDiv(
-                        vaultEndingETHBalance, vaultEndingStakesAmount
-                   ) 
-                );
+              (uint256 currentState, uint256 stakingEarningsShare) = calculateVariableWithdrawState(
+                    totalEarnings,
+                  variableToWithdrawnStakingEarnings[msg.sender]
+                );
                stakingShareAmount = stakingEarningsShare;
                variableToWithdrawnStakingEarningsInShares[msg.sender] =
                    currentState.mulDiv(vaultEndingStakesAmount, vaultEndingETHBalance);
                variableToWithdrawnStakingEarnings[msg.sender] = currentState;
            }

```