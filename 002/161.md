Amusing Flaxen Bull

Medium

# Invalid `variableSideCapacity` will prevent the vault from starting

### Summary

A missing check in the [initialize](https://github.com/sherlock-audit/2024-08-saffron-finance/blob/main/lido-fiv/contracts/LidoVault.sol#L276) function will permit the `variableSideCapacity` to be less than the `minimumDepositAmount`, which will prevent the vault from starting.

### Root Cause

In [initialize](https://github.com/sherlock-audit/2024-08-saffron-finance/blob/main/lido-fiv/contracts/LidoVault.sol#L276) there is a missing check if the `variableSideCapacity` is less than the `minimumDepositAmount`.

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

1. Create a `LidoVault` using the [VaultFactory::createVault()]() and pass as argument for `variableSideCapacity` value that is less than `0.01`.
2. Fixed side users will be able to deposit and withdraw.
3. Variable side users will not be able to deposit => the vault will not start.

### Impact

Core functionality of the protocol is broken.

### PoC

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.20;

import {Test} from "forge-std/Test.sol";

import {VaultFactory} from "src/VaultFactory.sol";
import {LidoVault} from "src/LidoVault.sol";
import {ILido} from "src/interfaces/ILido.sol";
import {MockLido} from "src/mocks/MockLido.sol";
import {MockLidoWithdrawalQueue} from "src/mocks/MockLidoWithdrawalQueue.sol";

contract PoC is Test {
    address payable constant LIDO = payable(0xae7ab96520DE3A18E5e111B5EaAb095312D7fE84);
    address payable constant LIDO_WITHDRAWAL_QUEUE = payable(0x889edC2eDab5f40e902b864aD4d7AdE8E412F9B1);

    VaultFactory vf;

    address user = makeAddr("user");
    address protocolFeeReceiver = makeAddr("protocolFeeReceiver");

    function setUp() public {
        deal(user, 10 ether);

        // Deploy the Lido mocks on the respective addresses
        deployCodeTo("MockLido", LIDO);

        // Deploy the vault factory
        vm.prank(protocolFeeReceiver);
        vf = new VaultFactory(100, 100);
    }

    function test() public {
        uint256 fixedSideCapacity = 1 ether;
        uint256 variableSideCapacity = 0.005 ether;
        uint256 duration = 7 days;
        uint256 vaultId = vf.nextVaultId();

        vf.createVault(fixedSideCapacity, duration, variableSideCapacity);
        (, address addr) = vf.vaultInfo(vaultId);
        LidoVault lv = LidoVault(payable(addr));
        uint256 minDepositAmount = lv.minimumDepositAmount();

        vm.prank(user);
        lv.deposit{value: 1 ether}(0); // Deposit as fixed side

        assert(ILido(LIDO).balanceOf(address(lv)) == 1 ether); // Successfully executed fixed deposit

        vm.prank(user);
        vm.expectRevert(bytes("MDA"));
        lv.deposit{value: variableSideCapacity}(1); // Deposit as variable side the set capacity

        vm.prank(user);
        vm.expectRevert(bytes("OED"));
        lv.deposit{value: minDepositAmount}(1);

        assert(lv.isStarted() == false); // The vault will never start
    }
}
```

### Mitigation

```diff
 function initialize(InitializationParams memory params) external {
        require(isFactoryCreated != true, "MBF");
        // Only run once by vault factory in atomic operation right after cloning, then fixedSideCapacity is set
        require(fixedSideCapacity == 0, "ORO");

        // Validate args
        require(params.vaultId != 0, "NEI");
        require(params.duration != 0, "NEI");
        require(params.fixedSideCapacity != 0, "NEI");
        require(params.variableSideCapacity != 0, "NEI");
+       require(params.variableSideCapacity >= minimumDepositAmount, "NEI");
        require(params.earlyExitFeeBps != 0, "NEI");
        require(params.protocolFeeReceiver != address(0), "NEI");

        require(params.fixedSideCapacity.mulDiv(minimumFixedDepositBps, 10_000) >= minimumDepositAmount, "IFC");

        // Initialize contract state variables
        id = params.vaultId;
        duration = params.duration;
        fixedSideCapacity = params.fixedSideCapacity;
        variableSideCapacity = params.variableSideCapacity;
        earlyExitFeeBps = params.earlyExitFeeBps;
        protocolFeeBps = params.protocolFeeBps;
        protocolFeeReceiver = params.protocolFeeReceiver;

        emit VaultInitialized(
            id, duration, variableSideCapacity, fixedSideCapacity, earlyExitFeeBps, protocolFeeBps, protocolFeeReceiver
        );
    }
```